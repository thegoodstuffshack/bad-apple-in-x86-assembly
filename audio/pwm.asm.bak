;; DOSBOX .com
;; modified from https://www.youtube.com/watch?v=4SBUrv7fXqI
;; and https://github.com/leonardo-ono/Assembly8086PlayPcmDigitizedSoundOnPCSpeakerTest
[bits 16]
[org 0x7c00]
jmp short start

;; ======================================
; nasm -f bin pwm.asm -o pwm.bin
; qemu-system-x86_64 -audiodev dsound,id=id -machine pcspk-audiodev=id pwm.bin
;; ======================================

start:
	;; SETUP
	mov [BOOT_DRIVE], dl
	
	xor ax, ax
	mov es, ax

	call load_audio	; 126 sectors
	
	mov al, 2
	call print
	
	mov bl, 0x4a; 16000 Hz sampling rate
	call change_timer_0
	
	; speaker on
	in al, 0x61
	or al, 3	; 0000 0011
	out 0x61, al
	
	;; MUSIC LOOP
.loop:
	;push word 
	push word 0x0000
	call music_segment
	
	mov al, 1
	call print
	
	push word 0x1000
	call music_segment
	
	mov al, 1
	call print
	
	push word 0x2000
	call music_segment
	
	jmp exit
	
;; ======================================
music_segment:
	push bp
	mov bp, sp

next_sample:
	mov dx, [es:046ch] ; int 8 counter address
delay:
	cmp dx, [es:046ch]
	jz delay

	; play 1 byte sample
	mov al, 0x90	; 1001 0000
	out 43h, al
	mov si, [sound_byte_index]

	mov bx, word [bp+4]
	mov es, bx
	mov bx, 0x7e00
	
	; es * 16 (data offset) + 0x7e00 + si (byte offset)
	mov al, byte [es:bx + si]
	shr al, 1
	out 42h, al	; send freq to speaker (essentially)

	xor ax, ax
	mov es, ax	; zero es for next PIT check

	mov al, 12
	call print
	
	; if keypress exit
	mov ah, 1
	int 16h
	jnz exit
	
	; increment index
	inc si
	cmp si, [sound_size]
	mov [sound_byte_index], si
	jb next_sample
	mov word [sound_byte_index], 0
	
	pop bp
ret 2
;; ======================================
exit:
	; speaker off
	in al, 0x61
	and al, 0b11111100
	out 0x61, al
	
	; restore original 18.2Hz timer
	mov bl, 0
	call change_timer_0
	
	jmp $
;; ======================================
change_timer_0:	; bl = timer divider
	cli
	mov al, 16h	; 0001 0110
	out 43h, al
	mov al, bl
	out 40h, al
	sti
ret
;; ======================================
print:	; al = printed character
	mov ah, 0x0e
	int 0x10
ret
;; ======================================
load_audio:
	mov ah, 0x02
	mov al, 63
	mov ch, 0
	mov cl, 2
	mov dh, 0
	mov dl, [BOOT_DRIVE]
	mov bx, 0x7e00
	int 0x13
	mov dl, 1
	jc disk_error
	mov ah, 0x02
	mov al, 63
	mov ch, 0
	mov cl, 1
	mov dh, 0
	mov dl, [BOOT_DRIVE]
	mov bx, 0xfc00
	int 0x13
	mov dl, 2
	jc disk_error
ret

disk_error:
	mov al, ah
	call print
	mov al, dl
	call print
	jmp $
;; ======================================

BOOT_DRIVE db 0
sound_byte_index dw 0

sound_size dw 0xFFFF	;65535
;; ======================================

times 510 -($-$$) db 0
dw 0xAA55
;; ======================================

;; data segment size: 128 sectors
sound_data1: incbin "raw/ba_0001.raw"
times 512 * 128 -($-sound_data1) db 0
sound_data2: incbin "raw/ba_0002.raw"
times 512 * 256 -($-sound_data1) db 0
sound_data3: incbin "raw/ba_0003.raw"
times 512 * 384 -($-sound_data1) db 0